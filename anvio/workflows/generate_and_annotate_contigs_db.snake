import anvio
import pandas as pd
import anvio.workflows as w

dirs_dict = w.get_dir_names(config)

# setting configuration for optional steps
run_remove_human_dna_using_centrifuge = w.A(["remove_human_dna_using_centrifuge", "run"], config)
# default is NOT running taxonomy with centrifuge
run_taxonomy_with_centrifuge = w.A(["run_centrifuge", "run"], config)
# default is running anvi_run_hmms
run_anvi_run_hmms = w.A(["anvi_run_hmms", "run"], config, default_value=True)
# default is running anvi_run_ncbi_cogs
run_anvi_run_ncbi_cogs = w.A(["anvi_run_ncbi_cogs", "run"], config, default_value=True)
# sanity check for centrifuge db
if run_taxonomy_with_centrifuge:
    if not w.A(["run_centrifuge", "db"], config):
        raise ConfigError("If you plan to run centrifuge, then you must "\
                          "provide a path for the centrifuge db in the "\
                          "config file. See documentation for more details.")

def get_raw_fasta(wildcards):
    '''
        Define the path to the input fasta files.

        Uses the config details to choose between the raw fasta file,
        the reformatted, and the output of the host contamination removal.
        This function also deals with the different cases of "reference mode"
        Vs. "assembly mode".
    '''
    if 'references_txt' in config:
        # in 'reference mode' the input is the reference fasta
        references_txt_file = config["references_txt"]
        filesnpaths.is_file_exists(references_txt_file)
        references_information = pd.read_csv(references_txt_file, sep='\t', index_col=0).to_dict(orient='index')
        contigs = references_information[wildcards.group]['path']
    else:
        # by default the input fasta is the assembly output
        contigs = dirs_dict["ASSEMBLY_DIR"] + "/%s/final.contigs.fa" % wildcards.group
    return contigs


rule annotate_contigs_database:
    '''
        This is a dummy rule and it is here just to guarantee that all
        the contigs annotations will run (according to what was requested
        in the config file). The main use is to use --until annotate_contigs_database
        if you just want a contigs databases with all the annotations.
    '''
    version: 1.0
    log: dirs_dict["LOGS_DIR"] + "/{group}-annotate_contigs_database.log"
    input: 
        # this is here just so snakemake would run the taxonomy before running this rule
        taxonomy = rules.import_taxonomy.output if run_taxonomy_with_centrifuge else ancient(rules.gen_contigs_db.output.db),
        # this is here just so snakemake would run the hmms before running this rule
        hmms = rules.anvi_run_hmms.output if run_anvi_run_hmms else ancient(rules.gen_contigs_db.output.db),
        # this is here just so snakemake would run the ncbi cogs before running this rule
        cogs = rules.anvi_run_ncbi_cogs.output if run_anvi_run_ncbi_cogs else ancient(rules.gen_contigs_db.output.db),
    output: touch(dirs_dict['CONTIGS_DIR'] + "/{group}-annotate_contigs_database.done")
    shell: "touch {output} >> {log} 2>&1"


rule reformat_fasta:
    '''
        Reformating the headers of the contigs fasta files.
        
        This is required to make sure taht the headers don't contain
        any charachters that anvi'o doesn't like.It give contigs
        meaningful names; so that if the group name is 'MYSAMPLE01', the
        contigs would look like this:
        > MYSAMPLE01_000000000001
        > MYSAMPLE01_000000000002
    '''
    version: 1.0
    log: dirs_dict["LOGS_DIR"] + "/{group}-reformat_fasta.log"
    input:
        contigs = get_raw_fasta
    output:
        # write protecting the contigs fasta file using protected() because
        # runnig the assembly is probably the most time consuming step and
        # we don't want anyone accidentaly deleting or changing this file.
        contigs = protected(dirs_dict["ASSEMBLY_DIR"] + "/{group}/{group}-contigs.fa"),
        report = dirs_dict["ASSEMBLY_DIR"] + "/{group}/{group}-reformat-report.txt"
    params: prefix = "{group}"
    threads: w.T(config, 'reformat_fasta')
    resources: nodes = w.T(config, 'reformat_fasta'),
    # FIXME: See the issue https://github.com/merenlab/MerenLab-workflows/issues/6
    #        we should convert everything in this file to wrappers, and commit them
    #        to the Snakemake repository.
    shell: "anvi-script-reformat-fasta {input} -o {output.contigs} -r {output.report} --simplify-names --prefix {params.prefix} >> {log} 2>&1"


def input_for_run_remove_human_dna_using_centrifuge(wildcards):
    ''' input fasta for the rule run_remove_human_dna_using_centrifuge'''
    # The raw fasta will be used if no formatting is needed
    contigs = get_raw_fasta(wildcards)

    if w.A(['reformat_fasta','run'], config, True):
        # by default, reformat fasta is ran
        contigs = rules.reformat_fasta.output.contigs

    return contigs


if run_remove_human_dna_using_centrifuge:

    # These rules will only run if the user asked for removal of Human contamination
    rule remove_human_dna_using_centrifuge:
        """ this is just a placeholder for now """
        version: 1.0
        log: dirs_dict["LOGS_DIR"] + "/{group}-remove-human-dna-using-centrifuge.log"
        input: input_for_run_remove_human_dna_using_centrifuge
        output: contigs = dirs_dict["ASSEMBLY_DIR"] + "/{group}/{group}-contigs-filtered.fa"
        threads: w.T(config, 'remove_human_dna_using_centrifuge')
        resources: nodes = w.T(config, 'remove_human_dna_using_centrifuge'),
        shell: "touch {output} >> {log} 2>&1"


def get_fasta(wildcards):
    '''
        Define the path to the input fasta files.

        The input hierarchy is as follows:
            output of remove_human_dna_using_centrifuge
            output of reformat_fasta
            raw fasta
        Meaning that if host contamination removal was done then it's 
        output is the input fasta, elif reformat_fasta was ran then it's
        output will be used, otherwise the raw fasta will be used.
    '''
    # By default (if no host contamination removal is requested)
    # Then the input will be the same as the one that would have been used
    # by the rule run_remove_human_dna_using_centrifuge
    contigs = input_for_run_remove_human_dna_using_centrifuge(wildcards)
    
    if run_remove_human_dna_using_centrifuge:
        # if host contamination removal is used, then use it's output
        contigs = rules.remove_human_dna_using_centrifuge.output.contigs

    return contigs


rule gen_contigs_db:
    """ Generates a contigs database using anvi-gen-contigs-database"""
    # Setting the version to the same as that of the contigs__version in anvi'o
    version: anvio.__contigs__version__
    log: dirs_dict["LOGS_DIR"] + "/{group}-gen_contigs_db.log"
    # depending on whether human contamination using centrifuge was done
    # or not, the input to this rule will be the raw assembly or the
    # filtered.
    input: get_fasta
    output:
        db = dirs_dict["CONTIGS_DIR"] + "/{group}-contigs.db"
    params: group = "{group}"
    threads: w.T(config, 'gen_contigs_db', 5)
    resources: nodes = w.T(config, 'gen_contigs_db', 5),
    shell: "anvi-gen-contigs-database -f {input} -o {output.db} -n {params.group} >> {log} 2>&1"

if run_taxonomy_with_centrifuge:
    # If the user wants taxonomy to be assigned with centrifuge
    # then these following rules would run.
    rule export_gene_calls:
        ''' Export gene calls and use for centrifuge'''
        version: 1.0
        log: dirs_dict["LOGS_DIR"] + "/{group}-export_gene_calls.log"
        # marking the input as ancient in order to ignore timestamps.
        input: ancient(rules.gen_contigs_db.output.db)
        # output is temporary. No need to keep this file.
        output: temp(dirs_dict["CONTIGS_DIR"] + "/{group}-gene-calls.fa")
        threads: w.T(config, 'run_taxonomy_with_centrifuge')
        resources: nodes = w.T(config, 'run_taxonomy_with_centrifuge'),
        shell: "anvi-get-dna-sequences-for-gene-calls -c {input} -o {output} >> {log} 2>&1"


    rule run_centrifuge:
        ''' Run centrifuge on the exported gene calls of the contigs.db'''
        version: 1.0
        log: dirs_dict["LOGS_DIR"] + "/{group}-run_centrifuge.log"
        input: rules.export_gene_calls.output
        output:
            hits = dirs_dict["CONTIGS_DIR"] + "/{group}-centrifuge_hits.tsv",
            report = dirs_dict["CONTIGS_DIR"] + "/{group}-centrifuge_report.tsv"
        params: db=config["run_centrifuge"]['db']
        threads: w.T(config, 'run_centrifuge', 5)
        resources: nodes = w.T(config, 'run_centrifuge', 5),
        shell: "centrifuge -f -x {params.db} {input} -S {output.hits} --report-file {output.report} --threads {threads} >> {log} 2>&1"


    rule import_taxonomy:
        ''' Run anvi-import-taxonomy'''
        version: 1.0
        log: dirs_dict["LOGS_DIR"] + "/{group}-import_taxonomy.log"
        input:
            hits = rules.run_centrifuge.output.hits,
            report = rules.run_centrifuge.output.report,
            # marking the contigs.db as ancient in order to ignore timestamps.
            contigs = ancient(rules.gen_contigs_db.output.db)
        # using a flag file because no file is created by this rule.
        # for more information see:
        # http://snakemake.readthedocs.io/en/stable/snakefiles/rules.html#flag-files
        output: touch(dirs_dict["CONTIGS_DIR"] + "/{group}-anvi_import_taxonomy.done")
        params: parser = "centrifuge"
        threads: w.T(config, 'import_taxonomy')
        resources: nodes = w.T(config, 'import_taxonomy'),
        shell: "anvi-import-taxonomy -c {input.contigs} -i {input.report} {input.hits} -p {params.parser} >> {log} 2>&1"


if run_anvi_run_hmms:
    rule anvi_run_hmms:
        """ Run anvi-run-hmms"""
        version: 1.0
        log: dirs_dict["LOGS_DIR"] + "/{group}-anvi_run_hmms.log"
        # marking the input as ancient in order to ignore timestamps.
        input: ancient(rules.gen_contigs_db.output.db)
        # using a snakemake flag file as an output since no file is generated
        # by the rule.
        output: touch(dirs_dict["CONTIGS_DIR"] + "/anvi_run_hmms-{group}.done")
        threads: w.T(config, 'anvi_run_hmms', 20)
        resources: nodes = w.T(config, 'anvi_run_hmms', 20),
        shell: "anvi-run-hmms -c {input} -T {threads} >> {log} 2>&1"


rule anvi_run_ncbi_cogs:
    version: anvio.__contigs__version__
    log: dirs_dict["LOGS_DIR"] + "/{group}-anvi_run_ncbi_cogs.log"
    input: ancient(rules.gen_contigs_db.output.db)
    output: touch(dirs_dict["CONTIGS_DIR"] + "/anvi_run_ncbi_cogs-{group}.done")
    params:
        # anvi-run-ncbi-cogs params. See anvi-run-ncbi-cogs help menu for more info.
        cogs_data_dir = w.B(config, 'anvi_run_ncbi_cogs', 'cogs_data_dir'),
        sensitive = w.B(config, 'anvi_run_ncbi_cogs', 'sensitive'),
        temporary_dir_path = w.B(config, 'anvi_run_ncbi_cogs', 'temporary_dir_path'),
        search_with = w.B(config, 'anvi_run_ncbi_cogs', 'search_with')
    threads: w.T(config, 'anvi_run_ncbi_cogs', 5)
    resources: nodes = w.T(config, 'gen_contigs_db', 5),
    shell: "anvi-run-ncbi-cogs -c {input} -T {threads} {params.cogs_data_dir} {params.sensitive} {params.temporary_dir_path} {params.search_with} >> {log} 2>&1"
